// 主图指标

// =============================================================================

// 均线

MA10  : MA(CLOSE,10);
MA30  : MA(CLOSE,30);
MA60  : MA(CLOSE,60);

VAR1  := (100-((90*(HHV(HIGH,21)-CLOSE))/(HHV(HIGH,21)-LLV(LOW,21))));
VAR2  := (100-MA(((100*(HHV(HIGH,6)-CLOSE))/(HHV(HIGH,6)-LLV(LOW,6))),34));
VAR3  := HHV((HIGH-LOW),100)/10;

DRAWTEXT(CROSS(VAR1,MA(VAR2,6)),LOW-VAR3,'买'),COLORRED;
DRAWTEXT(CROSS(MA(VAR2,6),VAR1),HIGH+VAR3,'卖'),COLORGREEN;

PARTLINE(MA(CLOSE,21)>MA(CLOSE,55),MA(CLOSE,55)),COLORRED,LINETHICK(5);
PARTLINE(MA(CLOSE,21)<=MA(CLOSE,55),MA(CLOSE,55)),COLORGREEN,LINETHICK(5);

// ---------------------------------------------------------

// 鳄鱼线

Y := (HIGH + LOW)/2;

鳄鱼的上唇  : REF(SMA(Y, 5, 1), 3), COLORGREEN;
鳄鱼的牙齿  : REF(SMA(Y, 8, 1), 5), COLORRED;
鳄鱼的下颚  : REF(SMA(Y, 13, 1), 8), COLORBLUE;

// ---------------------------------------------------------

// 副图指标

// =============================================================================

// MACD指标

DIFF  : EMA(CLOSE,12) - EMA(CLOSE,26);
DEA   : EMA(DIFF,9);
MACD  : 2*(DIFF-DEA), COLORSTICK;

L1    := EMA(CLOSE,3) - EMA(CLOSE,13);
L2    := EMA(L1,9);
DRAWTEXT(CROSS(L1,L2),DIFF*0.9,'买'),COLORRED;
DRAWTEXT(CROSS(L2,L1),DIFF*0.9,'卖'),COLORGREEN;

STICKLINE(CROSS(L1,L2),0,MACD,10,0),COLORRED;
STICKLINE(CROSS(L2,L1),0,MACD,10,0),COLORGREEN;

// ---------------------------------------------------------

// 顶底提示

VAR1  := EMA(((2*CLOSE+HIGH+LOW)/4-LLV(LOW,13))/(HHV(HIGH,13)-LLV(LOW,13))*100,13);
VAR2  := EMA(0.667*REF(VAR1,1)+0.333*VAR1,2);

STICKLINE(VAR1>VAR2,VAR1,VAR2,6,0),COLORRED;
STICKLINE(VAR1<=VAR2,VAR1,VAR2,6,0),COLORGREEN;

VAR3  := EMA(SMA(SMA((CLOSE-LLV(LOW,20))/(HHV(HIGH,20)-LLV(LOW,20))*100,3,1),3,1)/28.57,5);
VAR4  := 3*(SMA(SMA((CLOSE-LLV(LOW,20))/(HHV(HIGH,20)-LLV(LOW,20))*100,3,1),3,1)/28.57)-2*VAR3;

AA    := CROSS(VAR4,VAR3) AND VAR3<2.1 AND CLOSE>O;
BB    := CROSS(VAR3,VAR4) AND VAR3>2.1 AND CLOSE>O;

STICKLINE(AA,10,50,10,0),COLORRED;
STICKLINE(BB,90,51,10,0),COLORGREEN;

// ---------------------------------------------------------

// 鳄鱼线动量震荡指标(AO)

Y := (HIGH + LOW)/2;

AO: MA(Y, 5) - MA(Y, 34), COLORSTICK;
AC: AO - MA(AO, 5);

STICKLINE(AO >= REF(AO, 1), 0, AO, 1, 0), COLORGREEN;
STICKLINE(AO < REF(AO, 1), 0, AO, 1, 0), COLORRED;
